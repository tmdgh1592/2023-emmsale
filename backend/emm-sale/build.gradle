plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.13'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
}

group = 'com'
version = '0.0.1-SNAPSHOT'

sourceSets {
    documentTest {
        compileClasspath += sourceSets.main.output + sourceSets.test.output
        runtimeClasspath += sourceSets.main.output + sourceSets.test.output
    }
}

java {
    sourceCompatibility = '11'
}

configurations {
    asciidoctorExt
    compileOnly {
        extendsFrom annotationProcessor
    }

    documentTestImplementation.extendsFrom(implementation, testImplementation)
    documentTestRuntimeOnly.extendsFrom(runtimeOnly, testRuntimeOnly)
}

repositories {
    mavenCentral()
}

ext {
    snippetsDir = file('build/generated-snippets')
}

tasks.register('createDocument', Test) {
    testClassesDirs = sourceSets.documentTest.output.classesDirs
    classpath = sourceSets.documentTest.runtimeClasspath
    useJUnitPlatform()

    outputs.dir snippetsDir
}

asciidoctor {
    dependsOn createDocument
    sourceDir 'src/documentTest/asciidoc'
    configurations 'asciidoctorExt'
    inputs.dir snippetsDir
}

tasks.register('copyDocument', Copy) {
    dependsOn asciidoctor

    from file("build/docs/asciidoc")
    into file("src/main/resources/static/docs")
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.register('documentTest', Test) {
    dependsOn copyDocument
}

build {
    dependsOn copyDocument, test
}

bootJar {
    dependsOn asciidoctor
    from("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.cloud:spring-cloud-starter-aws:2.2.6.RELEASE'
    
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'com.mysql:mysql-connector-j'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    // documentTest
    documentTestImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'

    // jwt
    implementation 'io.jsonwebtoken:jjwt:0.9.1'

    // firebase
    implementation 'com.google.firebase:firebase-admin:9.2.0'
}
